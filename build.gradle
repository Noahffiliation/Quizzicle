plugins {
    id 'java'
    id 'antlr'
    id 'application'
    id 'jacoco'
    id 'idea'
    id "org.sonarqube" version "7.2.2.6593"
}

group = 'com.quizzicle'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def generatedSrcDir = file("src/generated/java")

sourceSets {
    main {
        java {
            srcDir generatedSrcDir
        }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7.2"
    implementation "org.antlr:antlr4-runtime:4.7.2"
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.10.2"
}

generateGrammarSource {
    maxHeapSize = "64m"
    // This creates the /com/quizzicle/ folder structure inside the build directory
    arguments += ["-visitor", "-package", "com.quizzicle"]
    outputDirectory = generatedSrcDir
}

application {
    mainClass = 'com.quizzicle.Test'
}

tasks.named("compileJava") {
    dependsOn generateGrammarSource
}

tasks.named("compileTestJava") {
    dependsOn generateGrammarSource
}

test {
    useJUnitPlatform()
}

tasks.named("clean") {
    delete generatedSrcDir
}

idea {
    module {
        sourceDirs += generatedSrcDir
        generatedSourceDirs += generatedSrcDir
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                'com/quizzicle/QuizzicleLexer*.class',
                'com/quizzicle/QuizzicleParser*.class',
                'com/quizzicle/QuizzicleBaseVisitor*.class',
                'com/quizzicle/QuizzicleVisitor*.class',
                'com/quizzicle/QuizzicleBaseListener*.class',
                'com/quizzicle/QuizzicleListener*.class'
        ])
    }))
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                'com/quizzicle/QuizzicleLexer*.class',
                'com/quizzicle/QuizzicleParser*.class',
                'com/quizzicle/QuizzicleBaseVisitor*.class',
                'com/quizzicle/QuizzicleVisitor*.class',
                'com/quizzicle/QuizzicleBaseListener*.class',
                'com/quizzicle/QuizzicleListener*.class'
        ])
    }))
}

check.dependsOn jacocoTestCoverageVerification

sonar {
    properties {
        property "sonar.projectKey", "Noahffiliation_Quizzicle"
        property "sonar.organization", "noahffiliation"
    }
}
